Maple3 ONLY BOARD description
*

=login
wait 代號：
send $[username]\n
doif $[password]
    wait 密碼：
    send $[password]\nn\n
endo
send \n\n\n\n

=main
send qqqqqqqq
wait 主功能表
till 呼叫器

=board
call main
send b\n
send s
wait 名稱
send $[board]\n\x20\x20
wait 板主：
wait 文  章  標  題

=board_list_last
call board $[board]
send 1\np\nq>
#wait m/\x0d>\x20+1\x20/
wait 板主：
wait \x0d>
till /
exit

=board_article_fetch_first
call board $[board]
send $[article]\n\n
wait \e[2J
till \e[0;34;46m
  or \e[34;46m
till \e[31;47m (
wait \e[m
exit

=board_article_fetch_next
send \n
till \e[0;34;46m
  or \e[34;46m
till \e[31;47m (
wait \e[m
exit

=board_article_fetch_last
send q
till 板主：
exit

=logoff
send eeeeeeeeeeee\nn\ny\ny\n\n\n

# 簽名檔 post 前就問
=article_post_raw
call board $[board]
send \x10$[title]\n0\n$[body]
# clean header
send \x13\x19\x19\x19\x19
# clean tailer (x-origin) 
# send \x14\x19\x19\x19\x19 # deemed bad practice by autrijus.
# save
send \x18s\n\x20
exit

=balloon
wait \e[1;33;46m★
till \x20\e[37;45m\x20
till \x20\e[m
exit

=balloon_reply
send \x12
wait 回應
send $[message]\n
wait [Y]
send \n
wait \e[37;45m
wait \e[m
exit

=balloon_send
call talk_users
send /
wait 姓名：
send $[receiver]\n
wait $[receiver]
send s
wait Call-In：
send $[message]\n
wait [Y]
send \n
wait \e[m
exit

=configure
call main
send c\n
wait 其他功能
till Call機

=getmood
call configure
send m\n
wait 目前心情\x20\e[1;31m\x20
till \x20\e[
wait 第幾項：
send \n
exit

=setmood
call configure
send m\n
wait 第幾項：
send 0\n$[mood]\n
exit

=mail
call main
send m\n
wait 電子郵件
till Call機

=talk
call main
send t\n
wait 聊天說話
till Call機

=talk_users
call talk
send u\n
wait 休閒聊天
till (TAB/f)

=talk_users_refresh
send \x15
wait 休閒聊天
till (TAB/f)
exit

=mail_send
call mail
send s\n
wait 代號：
send $[receiver]\n
wait 主題：
send $[subject]\n
wait 編輯文章
send $[content]\x18
wait 檔案處理
send s\n\n
wait 底稿
doif $[savelocal]
    send \n
else
    send n\n
endo
send \n

=group
call main
send $[group]\n
wait 看板列表
till \e[30;47m
send 1\n

=group_list_first
call group $[group]
send 1\npn
wait \e[18;1H
wait \e[30;47m
till \x20
exit

=group_list_next
send n
wait \e[30;47m
till \x20
exit

=chat_enter
call talk
send c\no\n\n
wait \x00\x00\x00\x00\x00

=chat_room
send /j $[room]\n
wait [$[room]]
exit

=chat_say
send $[message]\n
wait $[message]
exit

=chat_listen
wait ;1H\x00\x00\x00\x00\x00
till :\x20
  or *\x20
till \e[
exit

=chat_leave
send /bye\n
wait [C]
send c\n
goto main

=chat_msg
send /m $[caller] $[message]\n
wait $[message]
exit
