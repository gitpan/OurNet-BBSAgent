Maple2/SOB ONLY BOARD description
*

setv cursor	●
setv morehead	m/\e\[0?m\e\[(?:[\d;]+)m\x20\x20瀏覽\x20P./
setv moretail	m/\[q\]\e\[30m結束\x20\x20\x20\e\[0?m/
setv postsign	1 # 簽名檔在 post 後問
setv pager      扣機

=login
wait 註冊：
send $[username]\n$[password]\nn\nq\x20q\x20q\x20q\x20q
goto main

=main
send \e[D\e[D\e[D\e[D\e[D\e[D
wait 主功能表
till 扣機

=logoff
call main
send g\ny\n\n\n\n

# 假設在主選單及看板內, 都可以 select board
=board
send s
wait 名稱
send $[board]\n\x20\x20
wait 板主：
till 文章選讀

=board_list_last
call board $[board]
send 1\n\x24h\x20
wait 板主：
wait \x0d$[cursor]
till /
exit

=board_article_fetch_first
call board $[board]
send $[article]\n\n
till $[morehead]
wait (
till )
wait $[moretail]
exit

=board_article_fetch_next
send \x20
till $[morehead]
wait (
till )
wait $[moretail]
exit

=board_article_fetch_last
send qq
wait 文章選讀
exit

=board_list_author
call board $[board]
send a$[author]\n
wait $[cursor]
till 文章選讀
exit

=article_post_raw
call board $[board]
send \x10$[title]\n
doif $[postsign]
else
  send 0\n
endo
send $[body]
# clean tailer (x-origin)
# send \x14\x19\x19\x19 # delete by fascist autrijus
send \x18s\n
doif $[postsign]
  send 0\n
endo
send \x20
doif $[author]
    call article_edit_author $[author]
endo # ?
exit

=article_edit_author
send \x24E\x19\x19\x19\x19
# 1 empty 2 from 3 origin 4 --
send \x14\x19\x19
send \x18\n
wait 文章選讀
exit

=article_edit_raw
send E$[content]\x18
wait 檔案處理
send s\n
doif $[postsign]
  send 0\n
endo
send \x20
doif $[author]
    call article_edit_author $[author]
endo
exit

=balloon
wait \e[1;33;46m★
till \e[37;45m\x20
till \x20\e[0m
exit

=balloon_reply
send \x12
wait 回去：
send $[message]\n
wait [Y]
send \n
wait \e[m
exit

=balloon_send
call talk_users
send /
wait 姓名：
send $[receiver]\n
wait $[receiver]
send s
wait Call-In：
send $[message]\n
wait [Y]
send \n
wait \e[m
exit

=configure
call main
send c\n
wait 其他功能
till Call機

=getmood
call configure
send m\n
wait 目前心情\x20\e[1;31m\x20
till \x20\e[
wait 第幾項：
send \n
exit

=setmood
call configure
send m\n
wait 第幾項：
send 0\n$[mood]\n
exit

=mail
call main
send m\n
wait 電子郵件
till Call機

=talk
call main
send t\n
wait 聊天說話
till Call機

=talk_users
call talk
send u\n
wait 休閒聊天
till (TAB/f)

=talk_users_refresh
send \x15
wait 休閒聊天
till (TAB/f)
exit

=mail_send
call mail
send s\n
wait 代號：
send $[receiver]\n
wait 主題：
send $[subject]\n
wait 編輯文章
send $[content]\x18
wait 檔案處理
send s\n\n
wait 底稿
doif $[savelocal]
    send \n
else
    send n\n
endo
send \n


=chat_enter
call talk
send c\no\n\n
wait \x00\x00\x00\x00\x00

=chat_room
send /j $[room]\n
wait [$[room]]
exit

=chat_say
send $[message]\n
wait $[message]
exit

=chat_listen
wait ;1H\x00\x00\x00\x00\x00
till :\x20
  or *\x20
till \e[
exit

=chat_leave
send /bye\n
wait [C]
send c\n
goto main

=chat_msg
send /m $[caller] $[message]\n
wait $[message]
exit
